import org.mtr.mapping.FileHelper
import org.mtr.mapping.Versions

plugins {
	id "java"
	id "maven-publish"
	id "net.minecraftforge.gradle" version "+" apply false
}

subprojects {
	apply plugin: "java"
	apply plugin: "maven-publish"
	apply plugin: "net.minecraftforge.gradle"

	final minecraftVersion = getProperty("minecraftVersion").toString()
	FileHelper.copyTest(projectDir, "forge-${minecraftVersion}")

	minecraft {
		mappings channel: "official", version: minecraftVersion
	}

	sourceSets.main.resources { srcDir "src/generated/resources" }

	dependencies {
		implementation project(":common")
		minecraft "net.minecraftforge:forge:${minecraftVersion}-${Versions.getForgeVersion(minecraftVersion)}"
		testImplementation "org.junit.jupiter:junit-jupiter-api:+"
		testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:+"
		testImplementation "org.reflections:reflections:+"
	}

	java {
		toolchain {
			languageVersion.set(JavaLanguageVersion.of(getProperty("javaLanguageVersion").toString()))
		}
		withSourcesJar()
		withJavadocJar()
	}

	build {
		doLast {
			FileHelper.copyBuildFile(projectDir, "forge", minecraftVersion, project.version.toString())
		}
	}

	test {
		useJUnitPlatform()
		testLogging { showStandardStreams = true }
	}

	publishing {
		publications {
			maven(MavenPublication) {
				from components.java
			}
		}
	}
}
